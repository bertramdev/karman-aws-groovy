apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

group = 'com.bertramlabs.plugins'

version = '0.6.0'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3"
    }
}


repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url 'http://dl.bintray.com/bertramlabs/karman' }
}


configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
	provided 'org.codehaus.groovy:groovy-all:2.0.7'
    compile 'com.bertramlabs.plugins:karman-core:0.6.1'
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    compile 'org.apache.httpcomponents:httpcore:4.2'
    compile 'org.apache.httpcomponents:httpclient:4.2'
    compile 'com.amazonaws:aws-java-sdk:1.7.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'karman-aws-groovy'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'karman-aws-groovy'
                    description 'Karman Storage Provider interface for amazon web services (primarily S3)'
                    url 'https://github.com/bertramdev/karman-aws-groovy'
                    scm {
                        url 'https://github.com/bertramdev/karman-aws-groovy'
                        connection 'scm:https://bertramdev@github.com/bertramdev/karman-aws-groovy.git'
                        developerConnection 'scm:git://github.com/bertramdev/karman-aws-groovy.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

configurations {
    provided
}
 


//Define bintrayUser and bintrayKey in ~/.gradle/gradle.properties
bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'anonymous'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'key here'
    publications = ['maven']
    pkg {
        repo = 'karman'
        userOrg = 'bertramlabs'
        name = 'karman-aws-groovy'
        licenses = ['Apache-2.0']
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
